apply plugin: 'maven-publish'
apply plugin: 'com.android.library'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

android {
    ndkVersion "28.0.12433566"
    namespace "dev.arkbuilders.core"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 35
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
        singleVariant('debug') {
            withSourcesJar()
        }
    }
}

cargo {
    libname = "fs_storage"
    module = "../fs-storage"
    prebuiltToolchains = true
    targets = ["arm64", "x86", "x86_64", "arm"]
    profile = gradle.startParameter.taskNames.any { it.toLowerCase().contains("debug") } ? "debug" : "release"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'org.apache.tika:tika-core:2.4.0'
    implementation "com.google.dagger:dagger:2.50"
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    implementation 'com.github.wseemann:FFmpegMediaMetadataRetriever:1.0.14'

    def coilVersion = "2.4.0"
    implementation "io.coil-kt:coil:$coilVersion"
    implementation "io.coil-kt:coil-gif:$coilVersion"
    implementation "io.coil-kt:coil-svg:$coilVersion"
    implementation "io.coil-kt:coil-video:$coilVersion"

    testImplementation "junit:junit:4.13.2"
    testImplementation "io.mockk:mockk:1.13.7"
}

tasks.configureEach { task ->
    if ((task.name == 'mergeDebugJniLibFolders' || task.name == 'mergeReleaseJniLibFolders')) {
        task.dependsOn 'cargoBuild'
    }
}

def libVersion = scmVersion.version

publishing {
    publications {
        release(MavenPublication) {
            groupId = "dev.arkbuilders"
            artifactId = "core"
            version = libVersion
            afterEvaluate {
                from components.release
            }
        }
        debug(MavenPublication) {
            groupId = "dev.arkbuilders"
            artifactId = "core-debug"
            version = libVersion
            afterEvaluate {
                from components.debug
            }
        }
    }

    repositories {
        maven {
            name = "GithubPackages"
            url = "https://maven.pkg.github.com/ARK-Builders/ark-core"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }

    }
}